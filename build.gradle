plugins {
    id 'java'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.9.5'
    id 'eclipse'
}

repositories {
    mavenCentral()
}

group = 'com.ducnh.api'
version = '0.0.1'


def grpcVersion = '1.37.0'
dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

tasks.named('test') {
    useJUnitPlatform()
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = "com.google.protobuf:protoc:3.15.8"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.36.1"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc { }
        }
    }
}

sourceSets {
    main {
        proto {
            // In addition to the default "src/main/proto"
            srcDir "src/api/grpc"
            srcDir "src/api/java"
        }
    }
}

task cleanProtoGen {
    doFirst {
        delete('src/api/grpc')
        delete('src/api/java')
    }
}

clean.dependsOn cleanProtoGen

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'payment-gateway-api'
            from components.java
        }
    }
}